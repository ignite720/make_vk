cmake_minimum_required(VERSION 3.28)

file(READ ${CMAKE_SOURCE_DIR}/VK_VERSION PRJ_VERSION_RAW)
string(STRIP "${PRJ_VERSION_RAW}" PRJ_VERSION)

project(make_vk
    LANGUAGES C CXX
    VERSION ${PRJ_VERSION}
)

option(MAKE_VK_BUILD_TESTS "Build the tests" OFF)
option(MAKE_VK_ENABLE_INSTALL "Install ${PROJECT_NAME}" ${PROJECT_IS_TOP_LEVEL})

include(GNUInstallDirs)
set(MAKE_VK_TARGET_NAME ${PROJECT_NAME})
set(MAKE_VK_PACKAGE_NAME "${PROJECT_NAME}Targets")
set(MAKE_VK_COMPONENT_NAME "${PROJECT_NAME}_comp")
set(MAKE_VK_CONFIG_FILE "${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_NAME}Config.cmake")
set(MAKE_VK_CONFIG_VERSION_FILE "${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_NAME}ConfigVersion.cmake")
set(MAKE_VK_CONFIG_INSTALL_DESTINATION "${CMAKE_INSTALL_LIBDIR}/cmake/${PROJECT_NAME}")

add_library(${PROJECT_NAME} INTERFACE)
target_include_directories(${PROJECT_NAME} INTERFACE
    $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>
    $<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>
)

if(MAKE_VK_ENABLE_INSTALL)
    include(CMakePackageConfigHelpers)

    install(TARGETS ${MAKE_VK_TARGET_NAME} EXPORT ${MAKE_VK_PACKAGE_NAME})
    install(DIRECTORY "include" DESTINATION ${CMAKE_INSTALL_INCLUDEDIR})

    configure_package_config_file("${CMAKE_CURRENT_SOURCE_DIR}/cmake/Config.cmake.in" ${MAKE_VK_CONFIG_FILE} INSTALL_DESTINATION ${MAKE_VK_CONFIG_INSTALL_DESTINATION})
    write_basic_package_version_file(${MAKE_VK_CONFIG_VERSION_FILE} COMPATIBILITY SameMajorVersion)
    install(FILES ${MAKE_VK_CONFIG_FILE} ${MAKE_VK_CONFIG_VERSION_FILE} DESTINATION ${MAKE_VK_CONFIG_INSTALL_DESTINATION})
    install(EXPORT ${MAKE_VK_PACKAGE_NAME} DESTINATION ${MAKE_VK_CONFIG_INSTALL_DESTINATION})
endif()

if(MAKE_VK_BUILD_TESTS)
    enable_testing()
    add_subdirectory(tests)
endif()